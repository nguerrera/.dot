;; See lisp/ng-*.el for the main configuration
;; 
;; init.el's only job is to bootstrap use-package:
;; https://github.com/jwiegley/use-package
;;
;; We do so while avoiding calling (package-initialize), which is
;; slow, when the configuration is unchanged. The approach is inspired
;; by https://github.com/nilcons/emacs-use-package-fast, but simpler.
;; We don't bother with byte compilation, and instead write out a lock
;; file to remember the load path from a prior initialization.

(defvar ng/early-init-file "~/.emacs.d/lisp/ng-early-init.el"
  "The configuration file where package-archives are set and any
other configuration that would like to happen early before we
potentially hit the network.")

(defvar ng/init-file  "~/.emacs.d/lisp/ng-init.el"
  "The configuration file where package-archives are defined and
use-package calls are made.")

(defvar ng/package-lock-file "~/.emacs.d/.ng-package-lock.el"
  "The generated file where package load-paths are cached to be
reused when the config files have not changed.")

;; Set up load path for personal *.el files
(add-to-list 'load-path "~/.emacs.d/lisp")

;; Keep generated config in its own file (loaded at the end of this file)
(setq custom-file "~/.emacs.d/custom.el")

;; Don't initialize packages at startup, see the pains we go through
;; below to avoid this slow step.
(setq package-enable-at-startup nil)

;; load early config
(load ng/early-init-file)

;; Check if we have an up-to-date lock file and load it
;; if so. If not, boot using package.el.
(if (and (file-exists-p ng/package-lock-file)
         (file-newer-than-file-p ng/package-lock-file ng/early-init-file)
	 (file-newer-than-file-p ng/package-lock-file ng/init-file))
    (load ng/package-lock-file)
  (progn
    (require 'package)
    (package-initialize)
    (unless (package-installed-p 'use-package)
      (package-refresh-contents)
      (package-install 'use-package))
    (setq use-package-always-ensure t)
    (delete-file ng/package-lock-file)))  

;; load main config
(load ng/init-file)

;; save lock file to speed up future inits
(when use-package-always-ensure
  (let ((package-dir (file-truename package-user-dir)))
    (with-temp-file ng/package-lock-file
      (insert ";; This file is automatically generated to speed up package loading when the configuration hasn't changed.\n")
      (dolist (path load-path)
	(when (string-prefix-p package-dir path)
	  (insert (format "(add-to-list 'load-path \"%s\")\n" path)))))))

;; load generated config
(load custom-file)
